Import(['baselib', 'entitylib', 'env'])

lev_parser_c, lev_parser_h = CFile(
    source = 'lev_comp.y', target = ['lev_yacc.cc'], YACCFLAGS = ['-d'])
lev_lexer_c, = CFile(target = ['lev_lex.cc'], source = 'lev_comp.l')

dgn_parser_c, dgn_parser_h = CFile(
    source = 'dgn_comp.y', target = ['dgn_yacc.cc'], YACCFLAGS = ['-d'])
dgn_lexer_c, = CFile(target = ['dgn_lex.cc'], source = 'dgn_comp.l')

panic_obj = Object(source = 'panic.cc', CPPPATH = ['#include'])

Program(
    target = 'lev_comp', 
    source = [
        lev_parser_c, 
        lev_lexer_c, 
        'lev_main.cc', 
        panic_obj,
        baselib, 
        entitylib
    ], 
    CPPPATH = ['#include']
)

Program(target = 'dgn_comp', source = [dgn_parser_c, dgn_lexer_c, 'dgn_main.cc', baselib, entitylib], CPPPATH = ['#include'])

files = [
  'makedefs.cc',
]

objects = []
for file in files:
  objects.append(Object(source = [file], CPPPATH = [ '#include' ]));

Program(target = 'makedefs', source = objects + [baselib, entitylib, panic_obj])
